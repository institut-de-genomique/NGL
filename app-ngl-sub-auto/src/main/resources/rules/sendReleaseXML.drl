#created on: Jul 7, 2014
package birds

import java.util.Date;
import java.text.SimpleDateFormat;

#Select resource for workflow release
rule "select resources from nglsub for sendReleaseXML"
	@BirdsRule ( selectionRule )
	salience 200
	dialect 'java'
	no-loop
	when
		$input : InputSpecificationElement(name=="releaseToSend", treatmentSpecification.name=="sendReleaseXML", project.name == "SRA")
		$resourcesReferential : ResourcesReferential(name=="nglSubRef") 
		$device : JSONDevice() from $resourcesReferential.referentialDevice
		$rps : ResourcePropertiesSet(initialized==false, inputSpecificationElement==$input, resourcesReferential==$resourcesReferential)
	then
		Logger log = Logger.getLogger("ngl.sub.rules");
		$rps.initialize();
		Set<ResourceProperties> resourcePropertiesSet = $device.httpGetJSON(ProjectProperties.getProperty("server")+"/api/sra/submissions?stateCode=SUBR-SMD-IW", "bot");
		for(ResourceProperties rp : resourcePropertiesSet){
			String studyCode = rp.getProperty("studyCode");
			//Get study 
			Set<ResourceProperties> rpStudies = $device.httpGetJSON(ProjectProperties.getProperty("server")+"/api/sra/studies/"+studyCode, "bot");
			if(rpStudies.size()==1){
				ResourceProperties rpStudy = rpStudies.iterator().next();
				rp.setProperty("studyAccession",rpStudy.getProperty("accession"));
			}
		}
		log.debug("size resources "+resourcePropertiesSet.size());
		$rps.addResourcePropertiesSet(resourcePropertiesSet);
		modify($rps){};
end

rule "Set submission to SUBR-SMD-IP "
@BirdsRule( ExecutionService )
	dialect 'java'
	salience 500
	no-loop
	when
		$executionService : ExecutionService( status == ExecutionService.PRE_JOB_EXECUTION_STATUS)
		$job : Job( treatmentSpecification.specificationGroup == false,  treatmentSpecification.name != null , treatmentSpecification.name == "sendReleaseXML")
	then
		JobResource jobResource = $job.getUniqueJobResource("releaseToSend");
		String codeSubmission = jobResource.getProperty("code");
		JSONDevice jsonDevice = new JSONDevice();
		jsonDevice.httpPut(ProjectProperties.getProperty("server")+"/api/sra/submissions/"+codeSubmission+"/state", "{\"code\":\"SUBR-SMD-IP\"}", "bot");
end


rule "Define variable lineParmeter for sendReleaseXML specification"
	@BirdsRule(fsRule)
	dialect 'java'
	salience 300
	no-loop
    when
       $treatSpec : TreatmentSpecification(name=="sendReleaseXML", project.name=="SRA")
       $job : Job()
    then
       Logger log = Logger.getLogger("ngl.sub.rules");
       log.debug("Set parameter for spec sendReleaseXML");
       $job.setParameterValue("xmlSubmission",$job.getParameterValue("xmlSubmission").getValue().replace("%xmlSubmission%","submission.xml"));
       
       $job.setParameterValue("listAC",$job.getParameterValue("listAC").getValue().replace("%ebiResult%",$job.getUniqueJobResource("releaseToSend").getProperty("ebiResult")));
      
       //$job.setParameterValue("listAC",$job.getParameterValue("listAC").getValue().replace("%submissionCode%",$job.getUniqueJobResource("releaseToSend").getProperty("code")));
       //SimpleDateFormat dt = new SimpleDateFormat("yyyyMMddhhmm");
       //String dateSubmission = dt.format(new Date());
       //$job.setParameterValue("listAC",$job.getParameterValue("listAC").getValue().replace("%submissionDate%",dateSubmission));
       
       $job.setParameterValue("OptionCD",$job.getParameterValue("OptionCD").getValue().replace("%submissionDirectory%",$job.getUniqueJobResource("releaseToSend").getProperty("submissionDirectory")));
	
end

rule "POST DONE EXECUTION of sendReleaseXML"
	@BirdsRule( ExecutionService )
	dialect 'java'
	salience 200
	when
		$executionService : ExecutionService( status == ExecutionService.POST_JOB_EXECUTION_STATUS)
		$job : Job( treatmentSpecification!=null, treatmentSpecification.name == "sendReleaseXML", treatmentSpecification.project.name == "SRA", executionState == Job.DONE_STATUS )
	then
		Logger log = Logger.getLogger("ngl.sub.rules");
		//Call service treatment AC files results 
		JobResource jobResource = $job.getUniqueJobResource("releaseToSend");
		String codeSubmission = jobResource.getProperty("code");
		//String pathRelease = jobResource.getProperty("submissionDirectory")+File.separator+$job.getParameterValue("listAC").getValue();
		
		JSONDevice jsonDevice = new JSONDevice();
		
		try{
			jsonDevice.httpPut(ProjectProperties.getProperty("server")+"/api/sra/submissions/"+codeSubmission+"/state", "{\"code\":\"SUBR-SMD-F\"}", "bot");
		}catch(JSONDeviceException e){
			log.debug("Bad request "+e.getMessage());
		}
end

rule "POST ERROR EXECUTION of sendReleaseXML"
	@BirdsRule( ExecutionService )
	dialect 'java'
	salience 200
	when
		$executionService : ExecutionService( status == ExecutionService.POST_JOB_EXECUTION_STATUS)
		$job : Job( treatmentSpecification!=null, treatmentSpecification.name == "sendReleaseXML", treatmentSpecification.project.name == "SRA", executionState == Job.ERROR_STATUS )
	then
		Logger log = Logger.getLogger("ngl.sub.rules");
		JobResource jobResource = $job.getUniqueJobResource("releaseToSend");
		String codeSubmission = jobResource.getProperty("code");
		JSONDevice jsonDevice = new JSONDevice();
		try{
			jsonDevice.httpPut(ProjectProperties.getProperty("server")+"/api/sra/submissions/"+codeSubmission+"/state", "{\"code\":\"SUBR-SMD-FE\"}", "bot");
		}catch(JSONDeviceException e){
			log.debug("Bad request "+e.getMessage());
		}
		//Send mail error
		String errorMessage = "Error during executing job for sending meta release data : "+codeSubmission+".\n"+
			"Command line "+$job.getUnixCommand()+"\n"+
			"Check outputFile and log \n"+
			"STDOUT "+$job.getProperty(Job.STDOUT)+"\n"+
			"STDERR "+$job.getProperty(Job.STDERR)+"\n"+
			"SCHEDULER STDOUT "+$job.getProperty(JobScheduler.JOB_SCHEDULER_STD_OUTPUT_PATH+JobScheduler.JOB_SCHEDULER_STD_OUTPUT_FILE)+"\n"+
			"SCHEDULER STDERR "+$job.getProperty(JobScheduler.JOB_SCHEDULER_STD_ERROR_PATH+JobScheduler.JOB_SCHEDULER_STD_ERROR_FILE)+"\n"+
			"Exception "+$job.getProperty("Exception");
		
			SubmissionServicesFactory.getInstance().sendMail("william","ERROR Sending Meta Data Release Submission="+codeSubmission+" JobID="+$job.getId(),errorMessage);
end
